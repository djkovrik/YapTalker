plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC6-1"
    id "com.github.ben-manes.versions" version "0.17.0"
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration["compileSdkVersion"]
    buildToolsVersion globalConfiguration["buildToolsVersion"]

    defaultConfig {
        minSdkVersion globalConfiguration["minSdkVersion"]
        targetSdkVersion globalConfiguration["targetSdkVersion"]
        applicationId globalConfiguration["androidApplicationId"]
        versionCode globalConfiguration["androidVersionCode"]
        versionName globalConfiguration["androidVersionName"]
        testInstrumentationRunner globalConfiguration["testInstrumentationRunner"]
        testApplicationId globalConfiguration["testApplicationId"]
        flavorDimensions "variant"
        resConfigs "en", "ru"
    }
    productFlavors {
        dev {
            dimension "variant"
            minSdkVersion globalConfiguration["minSdkDevVersion"]
            multiDexEnabled true
        }
        prod {
            dimension "variant"
            minSdkVersion globalConfiguration["minSdkVersion"]
        }
    }
    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }
    buildTypes {
        beta {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFiles fileTree('proguard').asList().toArray()
            versionNameSuffix "-beta"
            matchingFallbacks = ['debug', 'release']
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFiles fileTree('proguard').asList().toArray()
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        it.buildConfigField 'String', 'VK_ACCESS_TOKEN', VK_ACCESS_TOKEN
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

detekt {
    version = "1.0.0.RC6-1"

    profile("main") {
        input = "$projectDir/src/main/java"
        config = "$rootDir/tools/static-analysis/detekt-config.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
        output = "$projectDir/build/reports/detekt.xml"
        baseline = "$projectDir/build/reports/baseline.xml"
    }
}

androidExtensions {
    experimental = true
}

dependencies {

    def androidDependencies = rootProject.ext.androidDependencies
    def coreDependencies = rootProject.ext.coreDependencies
    def rxDependencies = rootProject.ext.rxDependencies
    def networkingDependencies = rootProject.ext.networkingDependencies
    def uiDependencies = rootProject.ext.uiDependencies
    def testDependencies = rootProject.ext.testDependencies
    def androidTestDependencies = rootProject.ext.androidTestDependencies
    def supportingToolsDependencies = rootProject.ext.supportingToolsDependencies
    def developmentDependencies = rootProject.ext.developmentDependencies

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation androidDependencies.appCompat
    implementation androidDependencies.preferenceV7
    implementation androidDependencies.preferenceV14
    implementation androidDependencies.cardView
    implementation androidDependencies.design
    implementation androidDependencies.recyclerView
    implementation androidDependencies.constraintLayout

    implementation coreDependencies.dagger
    implementation coreDependencies.daggerAndroid
    implementation coreDependencies.daggerAndroidSupport
    implementation coreDependencies.moxy
    implementation coreDependencies.moxyAndroid
    implementation coreDependencies.moxyAppCompat
    implementation coreDependencies.cicerone
    implementation coreDependencies.anko
    implementation coreDependencies.ankoDesign

    kapt coreDependencies.daggerCompiler
    kapt coreDependencies.daggerAndroidCompiler
    kapt coreDependencies.moxyCompiler

    implementation rxDependencies.rxAndroid
    implementation rxDependencies.rxJava
    implementation rxDependencies.rxKotlin
    implementation rxDependencies.rxRelay
    implementation rxDependencies.rxLint
    implementation rxDependencies.rxBinding
    implementation rxDependencies.rxBindingAppCompat
    implementation rxDependencies.rxBindingRecyclerView
    implementation rxDependencies.rxBindingSupport
    implementation rxDependencies.autoDispose
    implementation rxDependencies.autoDisposeKotlin

    implementation networkingDependencies.jspoon
    implementation networkingDependencies.moshi
    implementation networkingDependencies.persistentCookies
    implementation networkingDependencies.picasso
    implementation networkingDependencies.retrofit
    implementation networkingDependencies.retrofitRxAdapter
    implementation networkingDependencies.retrofitMoshiConverter
    implementation networkingDependencies.retrofitScalarsConverter
    implementation networkingDependencies.retrofitJspoonConverter

    implementation uiDependencies.materialValues
    implementation(uiDependencies.materialDrawer) { transitive = true }
    implementation uiDependencies.iconicsCore
    implementation uiDependencies.iconicsView
    implementation uiDependencies.materialTypeface
    implementation uiDependencies.materialDialogs
    implementation uiDependencies.materialDialogsCommons
    implementation uiDependencies.toasty
    implementation uiDependencies.photoView

    testImplementation testDependencies.junit
    testImplementation testDependencies.mockito
    testImplementation testDependencies.mockitoKotlin
    testImplementation testDependencies.expekt

    androidTestImplementation androidTestDependencies.espresso
    androidTestImplementation androidTestDependencies.mockitoAndroid
    androidTestImplementation androidTestDependencies.mockitoKotlin

    implementation(supportingToolsDependencies.crashalytics) { transitive = true }
    implementation supportingToolsDependencies.firebaseCore

    implementation developmentDependencies.timber
    debugImplementation developmentDependencies.leakCanary
    releaseImplementation developmentDependencies.leakCanaryNoOp
    betaImplementation developmentDependencies.leakCanaryNoOp
    testImplementation developmentDependencies.leakCanaryNoOp
}

apply plugin: 'com.google.gms.google-services'