buildscript {
    ext.kotlin_version = '1.2.71'
    ext.goole_play_services_version = '4.2.0'
    ext.fabric_version = '1.25.4'
    repositories {
        google()
        maven { url 'https://maven.google.com/' }
        jcenter()
        maven { url "https://maven.fabric.io/public" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.2.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.gms:google-services:$goole_play_services_version"
        classpath "io.fabric.tools:gradle:$fabric_version"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC6-3"
    id "com.github.ben-manes.versions" version "0.20.0"
}

apply from: 'buildsystem/dependencies.gradle'

// Versioning convention from ribot:
// Major -> Millions, Minor -> Thousands, Bugfix -> Hundreds. E.g 1.3.72 == 1,003,072
// Current: 1.0.0 (001,000,000)
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

allprojects {
    ext {
        compileSdkVersion = 28
        minSdkVersion = 21
        targetSdkVersion = 28
        androidApplicationId = "com.sedsoftware.yaptalker"
        androidVersionCode = versionMajor * 1000000 + versionMinor * 1000 + versionPatch
        androidVersionName = "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner = "android.support.test.runner.AndroidJUnitRunner"
        testApplicationId = "com.sedsoftware.yaptalker.test"
    }
    repositories {
        google()
        maven { url 'https://maven.google.com/' }
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    configurations.all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                if (details.requested.name == 'kotlin-compiler-embeddable') {
                    details.useVersion "1.2.71"
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

detekt {
    version = "1.0.0.RC6-3"

    profile("main") {
        config = "$rootDir/tools/static-analysis/detekt-config.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
        output = "$projectDir/build/reports/detekt.xml"
        baseline = "$projectDir/build/reports/baseline.xml"
    }
}