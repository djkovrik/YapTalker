buildscript {
    ext.kotlin_version = '1.2.20'
    ext.goole_play_services_version = '3.1.2'
    ext.fabric_version = '1.25.1'
    repositories {
        google()
        jcenter()
        maven { url "https://maven.fabric.io/public" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.1.0-alpha08"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.gms:google-services:$goole_play_services_version"
        classpath "io.fabric.tools:gradle:$fabric_version"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC6-2"
    id "com.github.ben-manes.versions" version "0.17.0"
}

apply from: 'buildsystem/dependencies.gradle'

// Versioning convention from ribot:
// Major -> Millions, Minor -> Thousands, Bugfix -> Hundreds. E.g 1.3.72 == 1,003,072
// Current: 0.8.8 (000,008,008)
def versionMajor = 0
def versionMinor = 8
def versionPatch = 8

allprojects {
    ext {
        compileSdkVersion = 27
        buildToolsVersion = "27.0.3"
        minSdkVersion = 19
        minSdkDevVersion = 22
        targetSdkVersion = 27
        androidApplicationId = "com.sedsoftware.yaptalker"
        androidVersionCode = versionMajor * 1000000 + versionMinor * 1000 + versionPatch
        androidVersionName = "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner = "android.support.test.runner.AndroidJUnitRunner"
        testApplicationId = "com.sedsoftware.yaptalker.test"
    }
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "https://maven.google.com/" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

detekt {
    version = "1.0.0.RC6-2"

    profile("main") {
        config = "$rootDir/tools/static-analysis/detekt-config.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
        output = "$projectDir/build/reports/detekt.xml"
        baseline = "$projectDir/build/reports/baseline.xml"
    }
}